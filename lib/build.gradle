apply plugin: 'kotlin'

repositories {
	jcenter()
	mavenCentral()
	maven { url "https://jitpack.io" }
	maven { url "http://repository.jetbrains.com/all" }
}

configurations {
	ktlint
}

dependencies {
	compile 'com.github.jvm-redux:jvm-redux-api:1.1.0'
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	testCompile 'junit:junit:4.12'
	testCompile 'com.nhaarman:mockito-kotlin:0.3.1'
	testCompile 'org.jetbrains.spek:spek:1.0.9'
	testCompile 'org.mockito:mockito-core:2.0.86-beta'

	ktlint 'com.github.shyiko:ktlint:0.1.2'
}

task ktlint(type: JavaExec) {
	main = "com.github.shyiko.ktlint.Main"
	classpath = configurations.ktlint
	args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec) {
	main = "com.github.shyiko.ktlint.Main"
	classpath = configurations.ktlint
	args "-F", "src/**/*.kt"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

check.dependsOn ktlint
